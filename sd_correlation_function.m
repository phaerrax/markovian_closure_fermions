(* Read closure parameters from files *)
alpha = {
    Table[item[[1]] + I * item[[2]], {item, Import["mc_standard_parameters/alphas_6.dat"]}],
    Table[item[[1]] + I * item[[2]], {item, Import["mc_standard_parameters/alphas_8.dat"]}],
    Table[item[[1]] + I * item[[2]], {item, Import["mc_standard_parameters/alphas_10.dat"]}]
};

beta = {
    Table[item[[1]] + I * item[[2]], {item, Import["mc_standard_parameters/betas_6.dat"]}],
    Table[item[[1]] + I * item[[2]], {item, Import["mc_standard_parameters/betas_8.dat"]}],
    Table[item[[1]] + I * item[[2]], {item, Import["mc_standard_parameters/betas_10.dat"]}]
};

w = {
    Table[item[[1]] + I * item[[2]], {item, Import["mc_standard_parameters/coupls_6.dat"]}],
    Table[item[[1]] + I * item[[2]], {item, Import["mc_standard_parameters/coupls_8.dat"]}],
    Table[item[[1]] + I * item[[2]], {item, Import["mc_standard_parameters/coupls_10.dat"]}]
};

(* Matrix M in the correlation function generated by the closure: c(t) = w* exp(tM) w *)
fitmat[a_, b_] := DiagonalMatrix[a, 0] + DiagonalMatrix[b, 1] + DiagonalMatrix[b, -1]

(* Target correlation function (the unit-area semicircle) *)
semicirclecf[t_] := BesselJ[0, t] + BesselJ[2, t]
semicirclesd[x_] := 2 / Pi * Sqrt[1 - x^2]

(* Correlation function generated by the closure *)
c[n_, t_] := ConjugateTranspose[w[[n]]].MatrixExp[t * fitmat[ alpha[[n]], beta[[n]] ], w[[n]]]
closurecf[n_, t_] := Piecewise[{
    {c[n, t],             t >= 0},
    {Conjugate[c[n, -t]], t < 0}
}]

(* Spectral density corresponding to the closure correlation function *)
closuresd[n_, x_?NumericQ, upperbound_:1000000] := 1 / Pi * NIntegrate[Re[ E^(I x t) * c[n, t] ], {t, 0, upperbound}]

(* Difference between the generated and the target correlation functions *)
diff[n_, t_] := N[Abs[c[n,t] - semicirclecf[t]]]

(* TEST: what if we switch beta_j with -beta_j? *)
c2[n_, t_] := ConjugateTranspose[w[[n]]].MatrixExp[t * fitmat[ alpha[[n]], -beta[[n]] ], w[[n]]]
diff2[n_, t_] := N[Abs[c2[n,t] - semicirclecf[t]]]
